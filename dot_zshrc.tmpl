#!/bin/zsh
##
# ⚠️ WARNING: Don't manually `source` your .zshrc file! It can have unexpected
# side effects. Instead, to safely apply changes, use `znap restart`.
#
# Load Dotfile defaults
. "$HOME/.dotfiles/path_defaults.zsh"

ZNAP_ROOT="$HOME/.znap"
ZNAP_REPO="$ZNAP_ROOT/repo"
ZNAP_PLUGINS="$ZNAP_ROOT/plugins"
# Set the dir for znap itself
zstyle ':znap:*' repos-dir $ZNAP_REPO
# Set the plugins directory for znap
# zstyle ':znap:*' plugins-dir $ZNAP_PLUGINS

# Download Znap, if it's not there yet.
[[ -f $ZNAP_REPO/znap.zsh ]] ||
    git clone --depth 1 -- \
        https://github.com/marlonrichert/zsh-snap.git $ZNAP_REPO

source "$ZNAP_REPO/znap.zsh"  # Start Znap

# Load our alias file.
source "$DOTFILES/aliases.zsh"

# Load our completions file.
source "$DOTFILES/completions.zsh"

# Load our paths file.
source "$DOTFILES/paths.zsh"

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# load plugins
. "$DOTFILES/znap/plugins.zsh"

# TODO: not this, something more explicit
# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files

# Use Starship Prompt
eval "$(starship init zsh)"

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
# shellcheck disable=SC1090
[ -f ~/.localrc ] && . ~/.localrc
source "${XDG_CONFIG_HOME:-$HOME/.config}/asdf-direnv/zshrc"


# Add chezmoi shell completions
{{ completion "zsh" }}
